rule all:
    output: touch("{accession}.all")
    input: "data/{accession}/rnaspades/transcripts.fasta"

rule download_sra:
    output: 
        temp("data/{accession}/{accession}_1.fastq"),
        temp("data/{accession}/{accession}_2.fastq")
    resources: 
        cpus=6
    envmodules:
        "sratoolkit/2.9.6",
    shell: 
        "fasterq-dump {wildcards.accession} -O data/{wildcards.accession} -e {resources.cpus}"

rule fastp:
    input: 
        r1 = "data/{accession}/{accession}_1.fastq",
        r2 = "data/{accession}/{accession}_2.fastq"
    output: 
        r1 = "data/{accession}/{accession}_1.fastp.fastq.gz",
        r2 = "data/{accession}/{accession}_2.fastp.fastq.gz"
    resources:
        cpus = 7,
    envmodules:
        "fastp/0.20.1"
    shell: "fastp -i {input.r1} -I {input.r2} -o {output.r1} -O {output.r2} --thread {resources.cpus}"

rule rnaspades:
    input: 
        r1 = "data/{accession}/{accession}_1.fastp.fastq.gz",
        r2 = "data/{accession}/{accession}_2.fastp.fastq.gz"
    output: "data/{accession}/rnaspades/transcripts.fasta"
    resources:
        cpus = 32,
        mem_mb = 100000
    envmodules:
        "spades/3.14.1"
    shell: "rnaspades.py -1 {input.r1} -2 {input.r2} -o data/{wildcards.accession}/rnaspades --threads {resources.cpus} --memory 100"